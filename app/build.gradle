plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-parcelize'
    id 'com.google.firebase.appdistribution'
    id 'com.google.gms.google-services'
}

apply from: rootProject.file("gradle/testing.gradle")

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.myapplication"
        minSdk 23
        targetSdk 32
        versionCode 1
        versionName "1.0"

        /*
      To test Dagger hilt in android we have to create a HiltTest Runner, and make it copy application class
         and mention it here below
       */
        testInstrumentationRunner "com.myapplication.HiltTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            testCoverageEnabled true

            firebaseAppDistribution {
                groups= "Test"
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding true
        viewBinding true
    }
    testOptions {
        animationsDisabled true
        unitTests {
            includeAndroidResources = true
        }
    }
    hilt {
        enableTransformForLocalTests = true
    }

    packagingOptions {
        exclude 'META-INF/*'
    }
}

// Allow references to generated code
kapt {
    correctErrorTypes = true
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation 'androidx.databinding:databinding-common:7.1.2'
    implementation 'androidx.databinding:databinding-runtime:7.1.2'
    implementation 'com.google.ar:core:1.30.0'
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // define a BOM and its version
    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.9.0"))

    // define any required OkHttp artifacts without version
    implementation("com.squareup.okhttp3:okhttp")
    implementation("com.squareup.okhttp3:logging-interceptor")

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'

    implementation "com.squareup.retrofit2:converter-gson:2.9.0"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    implementation("io.coil-kt:coil:1.4.0")

}